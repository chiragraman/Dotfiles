#!/bin/zsh
# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# your project folder that we can `p [tab]` to
export PROJECTS=~/projects

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit && compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files

# evaluate dircolors if it exists
if [ "$(uname)" = "Linux" ]; then
  d=.dircolors
  test -r $d && eval "$(dircolors $d)"
elif [ "$(uname -s)" = "Darwin" ]; then
    eval $(gdircolors -p > ~/.dircolors)
fi

# Set the theme
ZSH_THEME="spaceship"
export SPACESHIP_CONFIG="$DOTFILES/zsh/spaceship-config.zsh"

# stash your environment variables in ~/.localrc. This means they'll stay
# out of your main dotfiles repository (which may be public, like this
# one), but you'll have access to them in your scripts.
[[ -a ~/.localrc ]] && source ~/.localrc